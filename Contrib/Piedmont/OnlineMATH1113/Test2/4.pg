## DESCRIPTION
## Piedmont University
## MATH 1113 Online Test 2
## Rational inequality
## ENDDESCRIPTION

## DBsubject(Algebra)
## DBchapter(Rational equations and functions)
## DBsection(Rational inequalities)
## Level(2)
## KEYWORDS('rational inequality', 'interval notation')
## Author(Doug Torrance)
## Institution(Piedmont)

DOCUMENT();

loadMacros(
  "PGstandard.pl",
  "MathObjects.pl",
  "parserRadioButtons.pl"
);

    
TEXT(beginproblem());

Context()->noreduce('(-x)-y');
Context()->noreduce('(-x)+y');

# idea:
# (p - r) / (x - p) <> (q - r) / (x - q)  (<> is placeholder for whichever
#                                          inequality symbol we have)
# is equivalent to
# (p - q) * (x - r) / ((x - p) * (x - q)) <> 0
# if num_sort(p, q, r) = (a, b, c), then solution is of form
# (-inf, a) U (b, c) or (a, b) U (c, inf)
# depending on sign of p - q and whether <> is < or >
# in particular:
# * (p > q and <) or (p < q and >) - start at -inf
# * (p > q and >) or (p < q and <) - end at inf
# if >= or <=, then we also need a bracket at r

sub get_solution {
    my $p = shift;
    my $q = shift;
    my $r = shift;
    my $symbol = shift;
    my ($a, $b, $c) = num_sort($p, $q, $r);
    my $ans;
    if (($p > $q and $symbol < 2) or ($p < $q and $symbol > 1)) {
	$ans = "\((-\infty, $a";
	if ($a == $r and $symbol % 2 == 1) {
	    $ans .= "]";
	} else {
	    $ans .= ")";
	}
	$ans .= "\cup";
	if ($b == $r and $symbol % 2 == 1) {
	    $ans .= "[";
	} else {
	    $ans .= "(";
	}
	$ans .= "$b,$c";
	if ($c == $r and $symbol % 2 == 1) {
	    $ans .= "]";
	} else {
	    $ans .= ")";
	}
    $ans .= "\)";
    } else {
    $ans = "\(";
	if ($a == $r and $symbol % 2 == 1) {
	    $ans .= "[";
	} else {
	    $ans .= "(";
	}
	$ans .= "$a,$b";
	if ($b == $r and $symbol % 2 == 1) {
	    $ans .= "]";
	} else {
	    $ans .= ")";
	}
	$ans .= "\cup";
	if ($c == $r and $symbol % 2 == 1) {
	    $ans .= "[";
	} else {
	    $ans .= "(";
	}
	$ans .= "$c,\infty)\)";
    }
    return $ans;
}

sub get_random_solution {
    my $symbol = random(0, 3);
    my $p = random(-6, 6);
    my $q;
    do {
	    $q = random(-6, 6);
    } until ($p != $q);
    my $r;
    do {
	    $r = random(-6, 6);
    } until ($p != $r and $q != $r);
    return get_solution($p, $q, $r, $symbol);
}

sub add_options {
    my @options = @_;
    while (@options < 4) {
        push(@options, get_random_solution());
        @options = uniq(@options);
    }
    return lex_sort(@options);
}


@symbols = ("<", "\leq", ">", "\geq");
$symbol = random(0, 3);
$p = random(-6, 6);
do {
    $q = random(-6, 6);
} until ($p != $q);
do {
    $r = random(-6, 6);
} until ($p != $r and $q != $r);

$left = Formula("($p - $r) / (x - $p)")->reduce->TeX;
$right = Formula("($q - $r) / (x - $q)")->reduce->TeX;
$ans = get_solution($p, $q, $r, $symbol);

$buttons = RadioButtons([add_options($ans)], $ans);

BEGIN_TEXT

Solve the following inequality.
$PAR

\[$left $symbols[$symbol] $right\].
$PAR
\{$buttons->buttons\}

END_TEXT
ANS($buttons->cmp);

ENDDOCUMENT();