## DESCRIPTION
##  Select functions which would extend to basis in P_2 
## ENDDESCRIPTION

## DBsubject(Linear algebra)
## DBchapter(Abstract vector spaces)
## DBsection(Basis and dimension)
## Date(14/03/2020)
## Institution(METU-NCC)
## Author(Benjamin Walter & Satoshi Kondo)
## Level(2)
## MO(1)
## Static(0)




##################################
#  Initialization

DOCUMENT();

loadMacros(
"PGstandard.pl",
"PGmatrixmacros.pl",
"PGchoicemacros.pl",
"MathObjects.pl",
);


sub pick {
  my ($k, @in) = @_;
  my @out = ();
  while (@out < $k) { push( @out, splice(@in, random(0,$#in), 1) ); }
  @out;
}



TEXT(beginproblem());

COMMENT('Two work, three do not');

#####################################
#  Setup

Context("Vector");
Context()->lists->set(Vector=>{open=>"(",close=>")"});
Context()->noreduce('(-x)-y');

sub Polynomial {
  my @v = @_;
  my $P = Formula("0");
  foreach (0..$#v) { $P = $P + Formula("$v[$_] * x^{$_}")->reduce; }

  return $P->reduce;
};



my @v; my @w; my @u; my $u; my @i;

@i = pick(2,(0..2));

foreach (0..2) { $v[$_] = random(1,3); } 
$v[$i[0]] = 0; $v[$i[1]] = -$v[$i[1]];
$v = Vector(@v);  $v = -$v if (random(0,1)==1);

foreach (0..2) { $w[$_] = random(1,3); } 
$w[$i[1]] = 0; $w[$i[0]] = -$w[$i[0]];
$w = Vector(@w);  $w = -$w if (random(0,1)==1);

my $n = $v x $w;    # perpendicular direction 

my @basis = ();     # vectors which make basis 
my @not = ();       # vectors which are in span

##

do { foreach (0..2) { $u[$_] = random(-3,3); } }
until ( !(Vector(@u)->isZero()) 
     && (Vector(@u) . $n != 0) ); 
$u = Vector(@u);
push(@basis,$u);

do { foreach (0..2) { $u[$_] = random(-3,3); } }
until ( !(Vector(@u)->isZero()) 
     && (Vector(@u) . $n != 0) 
     && !(Vector(@u)->isParallel($u))); 
$u = Vector(@u);
push(@basis,$u);

##

if (random(0,1)==1) { push(@not, $v); }
else                { push(@not, $w); }

push(@not, random(-1,1,2)*$v+random(-1,1,2)*$w); 

push(@not, Vector(0,0,0));

##

@basis = map {"\(\displaystyle p_3\!{\small(x)}=" . Polynomial(($_)->value)->TeX . "\vphantom{\big)} \)"} @basis;
@not   = map {"\(\displaystyle p_3\!{\small(x)}=" . Polynomial(($_)->value)->TeX . "\vphantom{\big)} \)"} @not;

$v = Polynomial($v->value)->TeX;
$w = Polynomial($w->value)->TeX;

$choice = new_checkbox_multiple_choice();
$choice->qa("Select all polynomials \(p_3\!{\small(x)}\) such that 
            \(\lbrace p_1\!{\small(x)},p_2\!{\small(x)},p_3\!{\small(x)}\rbrace\)
            is a basis of \(\mathcal{P}_2(\mathbb{R})\).",
            @basis);                # correct answers
$choice->extra(@not);               # incorrect answers

#####################################
#  Main text

Context()->texStrings;
BEGIN_TEXT

Let \(p_1\!{\small(x)},p_2\!{\small(x)} \in \mathcal{P}_2(\mathbb{R})\) be given by 
\[\begin{aligned}
  p_1\!{\small(x)} &= $v\\ 
  p_2\!{\small(x)} &= $w 
\end{aligned}\]
\{ $choice->print_q() \}
$BR
\{ $choice->print_a() \}

END_TEXT
Context()->normalStrings;

#####################################
#  Answer evaluation

$showPartialCorrectAnswers = 0;

ANS( checkbox_cmp( $choice->correct_ans() ) );


ENDDOCUMENT(); 
